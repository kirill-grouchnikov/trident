
apply plugin: 'com.jfrog.bintray'

def fs = File.separator
def bintrayProperties = new Properties()
bintrayProperties.load(new File("${System.properties['user.home']}${fs}.gradle${fs}bintray.properties").newInputStream())

def artifact = new Properties()
artifact.load(new File('./deploy.settings').newInputStream())

group = "org.pushingpixels"

bintray {
    user = bintrayProperties.getProperty("bintray.user")
    key = bintrayProperties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = project.properties.name
        websiteUrl = artifact.siteUrl
        vcsUrl = artifact.gitUrl
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = project.properties.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
